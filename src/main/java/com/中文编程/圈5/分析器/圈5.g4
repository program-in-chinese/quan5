grammar 圈5;
程序: 声明+;

声明
    :   赋值声明
    |   条件声明
    |   求值声明
    |   T新行
    ;

条件声明
    :   '如果' 表达式 声明
    ;

赋值声明
    : T变量名 '=' 表达式 T新行
    ;

求值声明
    : 表达式 T新行
    ;

表达式
    : 等同判断表达式;

等同判断表达式
    :   比较表达式
    |   等同判断表达式 '==' 比较表达式
    |   等同判断表达式 '为' 比较表达式
    |   等同判断表达式 '!=' 比较表达式
    |   等同判断表达式 '≠' 比较表达式
    ;

比较表达式
    :   求和表达式
    |   比较表达式 '<' 求和表达式
    |   比较表达式 '>' 求和表达式
    |   比较表达式 '<=' 求和表达式
    |   比较表达式 '>=' 求和表达式
    |   比较表达式 '≤' 求和表达式
    |   比较表达式 '≥' 求和表达式
    ;

求和表达式
  : 求积表达式
  | 求和表达式 '+' 求积表达式
  | 求和表达式 '-' 求积表达式
  ;

求积表达式
  : 最小表达式
  | 求积表达式 '*' 最小表达式
  | 求积表达式 '/' 最小表达式
  | 求积表达式 '×' 最小表达式
  | 求积表达式 '÷' 最小表达式
  ;

最小表达式
  : 字面量
  | '(' 表达式 ')'
  ;

字面量
  : T数
  | T变量名
  ;

T为: '为';
T变量名: ('a' .. 'z' | 'A' .. 'Z' | '\u4E00'..'\u9FA5' | '\uF900'..'\uFA2D')+;
T数
 : [0-9]+
 ;
T加 : '+';
T減 : '-';
T乘 : '*';
T数乘: '×';
T除 : '/';
T数除: '÷';
T相等: '==';
T大于: '>';
T小于: '<';
T大于等于: '>=';
T数大于等于: '≥';
T小于等于: '<=';
T数小于等于: '≤';
T不等: '!=';
T数不等: '≠';
T新行: '\r'?'\n';
T空格: [ ]+ ->skip;